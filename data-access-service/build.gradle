plugins {
    id 'java'
    id 'jacoco'
    id 'io.freefair.lombok' version '8.13.1'
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
    id 'uk.gov.laa.springboot.laa-spring-boot-gradle-plugin' version '1.0.4'
    id 'com.avast.gradle.docker-compose' version '0.17.12'
}

dockerCompose.isRequiredBy(integrationTest)

dockerCompose {
    useComposeFiles = ['../docker-compose.yml']
    // workaround https://github.com/avast/gradle-docker-compose-plugin/issues/435
    def dockerPath = ["/usr/bin/docker", "/usr/local/bin/docker"].find {
        new File(it).exists()
    }
    if (dockerPath) {
        dockerExecutable.set(dockerPath)
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.12"
}

//so that we load properties from gradle properties
task copyFileFromRootProject(type: Copy) {
    from "${rootProject.projectDir}/gradle.properties"
    into 'build/resources/main'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/AccessApp.class'])
        }))
    }
}

dependencies {
    implementation project(':data-access-api')
    implementation project(':data-access-shared')

    implementation 'org.springframework.security:spring-security-core:6.5.5'
    implementation 'org.springframework:spring-core:6.2.11'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation "com.vladmihalcea:hibernate-types-60:2.20.0"
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'com.google.code.gson', module: 'gson'
    }

    //fix https://security.snyk.io/vuln/SNYK-JAVA-IONETTY-12485149
    implementation 'io.netty:netty-codec-http:4.1.125.Final'
    //fix https://security.snyk.io/vuln/SNYK-JAVA-IONETTY-12485150
    implementation 'io.netty:netty-codec-http2:4.1.125.Final'
    //fix https://security.snyk.io/vuln/SNYK-JAVA-ORGAPACHETOMCATEMBED-13733966
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:10.1.48'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8'

    implementation 'com.azure.spring:spring-cloud-azure-starter-active-directory:5.22.0'
    implementation 'com.fasterxml.uuid:java-uuid-generator:5.1.0'

    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
    implementation 'org.javers:javers-core:7.8.0'

    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly "org.flywaydb:flyway-database-postgresql"

    testImplementation 'org.instancio:instancio-junit:5.5.1'
    testImplementation 'net.datafaker:datafaker:2.5.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation "org.testcontainers:testcontainers-junit-jupiter:2.0.1"
    testImplementation 'org.testcontainers:postgresql:1.21.3'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers:3.5.7'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Security fix for SNYK-JAVA-ORGAPACHECOMMONS-10734078 (CVE-2025-48924)
    implementation 'org.apache.commons:commons-lang3:3.18.0'
}
