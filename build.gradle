plugins {
    // Apply JaCoCo plugin at the root for aggregation
    id 'jacoco'
    // Release Management Plugin
    id 'net.researchgate.release' version '3.1.0'
}

subprojects {
    apply plugin: 'jacoco'
    group = 'uk.gov.justice.laa.data.access'

    release {3444
        tagTemplate = '$name-$version'
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacoco {
        toolVersion = "0.8.12"
    }

    tasks.withType(JacocoReport).configureEach { reportTask ->
        if (reportTask.name == 'jacocoTestReport') {
            reports {
                xml.required = true
                csv.required = false
                html.required = true
            }
        }
    }

    afterEvaluate {
        if (plugins.hasPlugin('checkstyle')) {
            checkstyle {
                toolVersion = '10.26.0'
            }
        }
    }
}
afterEvaluate {
    repositories {
        mavenCentral()
    }
    jacoco {
        toolVersion = "0.8.12"
    }

    tasks.register('jacocoAggregatedReport', JacocoReport) {
        description = 'Generates an aggregated JaCoCo report for all modules.'
        group = 'verification'

        dependsOn subprojects.collectMany { it.tasks.withType(Test) }

        sourceDirectories.from {
            subprojects.collect { project ->
                def javaExtension = project.extensions.findByType(JavaPluginExtension)
                if (javaExtension && project.name != 'data-access-api') {
                    return javaExtension.sourceSets.main.allSource.srcDirs
                }
            }.flatten().findAll()
        }

        classDirectories.from {
            subprojects.collect { project ->
                def javaExtension = project.extensions.findByType(JavaPluginExtension)

                if (javaExtension && project.name != 'data-access-api') {
                    return javaExtension.sourceSets.main.output.classesDirs.getFiles().collect { dir ->
                        project.fileTree(dir: dir) {
                            exclude('**/AccessApp.class')
                            exclude('**/generated/**')
                            exclude('**/config/*')
                        }
                    }
                }
            }.flatten().findAll()
        }

        executionData.from {
            subprojects.collect { project ->
                project.fileTree(
                        dir: "${project.buildDir}/jacoco",
                        includes: ["*.exec"]
                )
            }.flatten().findAll()
        }

        reports {
            xml.required = true
            csv.required = false
            html.required = true
        }
    }
}
