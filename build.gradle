plugins {
    id 'net.researchgate.release' version '3.1.0'
    id("java")
    id("org.openrewrite.rewrite") version("7.25.0")
}

rewrite {
    activeRecipe("org.openrewrite.java.migrate.UpgradeToJava25")
    setExportDatatables(true)
}

repositories {
    mavenCentral()
}

def requiredJavaVersion = 25
def currentJavaVersion = JavaVersion.current().majorVersion.toInteger()

if (currentJavaVersion != requiredJavaVersion) {
    throw new GradleException(
            "This project requires Java ${requiredJavaVersion}. " +
                    "Current Java version is ${currentJavaVersion}. " +
                    "Please update your JAVA_HOME or use the correct JDK."
    )
}

configurations {
    jacocoAnt
}

dependencies {
    rewrite(platform("org.openrewrite.recipe:rewrite-recipe-bom:latest.release"))
    rewrite("org.openrewrite.recipe:rewrite-migrate-java")
    jacocoAnt "org.jacoco:org.jacoco.ant:0.8.14"
}

subprojects {
    group = 'uk.gov.justice.laa.data.access'

    pluginManager.withPlugin('java') {
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(25)
            }
        }
    }

    release {
        tagTemplate = '$name-$version'
    }

    afterEvaluate {
        if (plugins.hasPlugin('checkstyle')) {
            checkstyle {
                toolVersion = '10.26.0'
            }
        }
    }
}

tasks.register('jacocoAggregatedReport', JacocoReport) {
    description = 'Generates JaCoCo report from data-access-service module'
    group = 'verification'

    dependsOn ':data-access-service:test'

    jacocoClasspath = configurations.jacocoAnt

    def serviceProject = project(':data-access-service')

    sourceDirectories = files("${serviceProject.projectDir}/src/main/java")

    classDirectories = fileTree(
            dir: "${serviceProject.buildDir}/classes/java/main",
            excludes: ['**/AccessApp.class', '**/generated/**', '**/config/**']
    )

    executionData = fileTree(
            dir: "${serviceProject.buildDir}/jacoco",
            includes: ["test.exec", "*.exec"]
    )

    reports {
        xml.required = true
        xml.outputLocation = layout.buildDirectory.file("reports/jacoco/aggregated.xml").get().asFile
        html.required = true
        html.outputLocation = layout.buildDirectory.dir("reports/jacoco/html").get().asFile
        csv.required = false
    }
}
