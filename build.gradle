plugins {
    // Apply JaCoCo plugin at the root for aggregation
    id 'jacoco'
    // Release Management Plugin
    id 'net.researchgate.release' version '3.1.0'
}

subprojects {
    apply plugin: 'jacoco'
    group = 'uk.gov.justice.laa.data.access'

    release {3444
        tagTemplate = '$name-$version'
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacoco {
        toolVersion = "0.8.12"
    }

    tasks.withType(JacocoReport).configureEach { reportTask ->
        if (reportTask.name == 'jacocoTestReport') {
            reports {
                xml.required = true
                csv.required = false
                html.required = true
            }
        }
    }

    afterEvaluate {
        if (plugins.hasPlugin('checkstyle')) {
            checkstyle {
                toolVersion = '10.26.0'
            }
        }
    }
}
afterEvaluate {
    jacoco {
        toolVersion = "0.8.12"
    }

    tasks.register('jacocoAggregatedReport', JacocoReport) {
        description = 'Generates an aggregated JaCoCo report for all modules.'
        group = 'verification'

        dependsOn subprojects.collectMany { it.tasks.withType(Test) }

        subprojects.each { project ->
            def projectClasses = project.extensions.getByType(SourceSetContainer).main.output.classesDirs.files
            def projectExecutionData = project.fileTree(
                    dir: "${project.buildDir}/jacoco",
                    includes: ["*.exec"]
            )

            classDirectories.from projectClasses
            executionData.from projectExecutionData
        }

        reports {
            xml.required = true
            csv.required = false
            html.required = true
        }
    }
}
