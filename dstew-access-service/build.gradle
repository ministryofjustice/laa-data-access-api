plugins {
    id 'io.freefair.lombok' version '8.13.1'
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
    id 'uk.gov.laa.ccms.springboot.laa-ccms-spring-boot-gradle-plugin'
    id 'com.avast.gradle.docker-compose' version '0.17.12'
}

dockerCompose.isRequiredBy(test)
dockerCompose.isRequiredBy(integrationTest)

dockerCompose {
    useComposeFiles = ['../docker-compose.yml']
    // workaround https://github.com/avast/gradle-docker-compose-plugin/issues/435
    def dockerPath = ["/usr/bin/docker", "/usr/local/bin/docker"].find {
        new File(it).exists()
    }
    if (dockerPath) {
        dockerExecutable.set(dockerPath)
    }
}

test {
    useJUnitPlatform()
    finalizedBy(jacocoTestReport)
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/AccessApplication.class'])
        }))
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/AccessApplication.class'])
        }))
    }
}

dependencies {
    implementation project(':dstew-access-api')
    implementation project(':dstew-access-shared')

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'com.google.code.gson', module: 'gson'
    }

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8'

    implementation "io.awspring.cloud:spring-cloud-aws-starter-sqs:3.3.1"

    // Spring Boot Starter for Microsoft Entra (provides auto-configuration specific to Entra)
    implementation 'com.azure.spring:spring-cloud-azure-starter-active-directory:5.22.0'

    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    implementation 'org.postgresql:postgresql:42.7.6'
    implementation 'org.javers:javers-core:7.8.0' // instead of `implementation 'org.javers:javers-persistence-sql:7.8.0'`

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    runtimeOnly "org.flywaydb:flyway-database-postgresql"

//    developmentOnly 'org.springframework.boot:spring-boot-devtools'
}
